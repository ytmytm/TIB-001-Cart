









               OPT 1

               ORG $1000
STARTUP        EQU $F0
TTLSEC         EQU $F7
TO             EQU $FB
CLUSTER        EQU $35A
LOADAD         EQU $36A
LENGTH         EQU $35E
BYTESLEFT      EQU $362

LOADFILE       EQU $8009                ; ACTUAL ROM JUMP VECTOR
SAVEFILE       EQU $800C                ; JMP LOADFILE  ETC
FORMATDISK     EQU $800F
DISPLAYDIR     EQU $8012
READSEC        EQU $8015
WATCHDOG       EQU $8018
READSECTORS    EQU $801B
WRITESEC       EQU $801E
READSTAT       EQU $8021
SCRATCH        EQU $8024
RENAME         EQU $8027
FORMAT         EQU $802A
INIT           EQU $802D
SETUPSEC       EQU $8030
SPECIFY        EQU $8033
RECAL          EQU $8036
SETSPACE       EQU $8039
GETNEXTCLUS    EQU $803C
ENFILE         EQU $803F
MARKFAT        EQU $8042
FINDFAT        EQU $8045
FINDNEXTFAT    EQU $8048
WRITEFATS      EQU $804B
CLEARFATS      EQU $804E
CALCFIRST      EQU $8051
GETFATS        EQU $8054
SEEK           EQU $8057
FINDFILE       EQU $805A
WRITEDIR       EQU $805D
READDIR        EQU $8060
SAVERELOC      EQU $8063
SHOWSIZE       EQU $8066
SHOWERR        EQU $8069
SHOWBYTESFREE  EQU $806C
BN2DEC         EQU $806F
STRIPSP        EQU $8072
SEARCH         EQU $8075
FINDBLANK      EQU $8078
PADOUT         EQU $807B
WOFF           EQU $807E
SPARE          EQU $FFFF
;    ENT


               LDA #6
               STA $D020
               STA $D021
               LDY #0
.LOOP          LDA $1800,Y
               STA $0800,Y
               LDA $1900,Y
               STA $0900,Y
               LDA $1A00,Y
               STA $0A00,Y
               LDA $1B00,Y
               STA $0B00,Y
               DEY
               BNE .LOOP
               LDA #$1B
               STA $D011
               LDA #$C3
               STA $DD00
               LDA #$08
               STA $D016
               LDA #$13
               STA $D018
main           JSR DECOMP
               JSR PRINTSCR
               LDA #1
               STA MAX
               LDA #0
               STA FILENUM
               SEI
MAIN           JSR DELAY
               JSR PULSE
               LDX #1
.SCAN          LDA KEYNUMS,X
               JSR KEYSCAN
               BCS GOT
               DEX
               BPL .SCAN
               JSR GETKEYS
               BCC MAIN
               LDA FILENUM
               TAX
               LDA #0
               STA FILENUM
               JMP GOT
               JMP MAIN
GOT            LDA KEYNUMS,X
               JSR KEYSCAN
               BCS GOT

               CPX #0
               BNE GAMES

               JSR PRINTSC2
               LDA #7
               STA MAX
UTILSLOP       JSR DELAY
               JSR PULSE
               LDX #8
.SCAN          LDA KEYNUMS,X
               JSR KEYSCAN
               BCS .GOT
               DEX
               BPL .SCAN
               JSR GETKEYS
               BCC UTILSLOP
               LDA FILENUM
               TAX
               LDA #0
               STA FILENUM
               JMP .GOT
               JMP UTILSLOP
.GOT           TXA
               CPX #0
               BEQ .BASIC
               CPX #8
               BNE .ok
               JMP main
.ok            ADD #$7
               STA FILENUM
               LDA #$15
               STA $D018
               JMP LOADIT
.BASIC
               LDA #$15
               STA $D018
               JMP ($A000)



GAMES          JSR PRINTSC3
               LDA #8
               STA MAX

GAMESLOOP      JSR DELAY
               JSR PULSE
               LDX #8
.SCAN          LDA KEYNUMS,X
               JSR KEYSCAN
               BCS .GOT
               DEX
               BPL .SCAN
               JSR GETKEYS
               BCC GAMESLOOP
               LDA FILENUM
               TAX
               LDA #0
               STA FILENUM
               JMP .GOT

               JMP GAMESLOOP
.GOT           CPX #8
               BNE .ok
               JMP main
.ok            STX FILENUM
               JMP LOADIT




KEYNUMS        DFB 56,59
               DFB 8,11,16,19,24,27,32
               DFB $FF
               DFB 10,28,20

DELAY          LDA $D012
               CMP #0
               BNE DELAY
.WAIT          LDA $D012
               BEQ .WAIT
               RTS

PULSE
               DEC TOCK
               BPL .OK
               LDA #6
               STA TOCK
               LDX COLOUR
               LDA COL,X
               LDY #$28*5
.CLTA                                   ;  STA $D800-1,Y
;  DEY
;   BNE .CLTA
               DEC COLOUR
               BPL .OK
               LDA #6
               STA COLOUR
.OK
               DEC TICK
               BPL .NOCHG
               LDA #8
               STA TICK
               LDA FILENUM
               ASL 
               TAY
               LDX COLNUM
               LDA LOC,Y
               STA STARTUP
               LDA LOC+1,Y
               STA STARTUP+1
               LDY #39
               LDA COL,X
               BNE .NEX
               LDX #0
               STX COLNUM
               LDA COL,X

.NEX
               STA (STARTUP),Y
               DEY
               BPL .NEX
               INC COLNUM
.NOCHG         RTS




KEYSCAN        STY KEYT
               PHA
               LSR 
               LSR 
               LSR 
               TAY
               LDA COLOM,Y
               STA $DC00
               PLA
               AND #$07
               TAY
               LDA KROW,Y
               AND $DC01
               BNE NOTPRESS
               LDA #$FF
               STA $DC00
               LDA $DC01
               AND KROW,Y
               BEQ NOTPRESS
               SEC
               LDY KEYT
               RTS

NOTPRESS       CLC
GOTKEY         LDY KEYT
               RTS

COLOM          DFB $FE,$FD,$FB,$F7
               DFB $EF,$DF,$BF,$7F

KROW           DFB $01,$02,$04,$08
               DFB $10,$20,$40,$80
LOADIT
               LDX FILENUM
               LDA FILELENS,X
               STA NAMLEN
               TXA
               ASL 
               TAX
               LDA FILEPTRS,X
               STA LONAM
               LDA FILEPTRS+1,X
               STA HINAM
               LDA START,X
               STA STARTUP
               LDA START+1,X
               STA STARTUP+1

               LDY #60
.LOOP          LDA PRG,Y
               STA $0200,Y
               DEY
               BPL .LOOP
               LDA #15
               LDX #9
               LDY #15
               JSR $FFBA                ; SETLFS
               LDA NAMLEN
               LDX LONAM
               LDY HINAM
               JSR $FFBD                ; SETNAM
               LDA #0
               LDX #$FF
               LDY #$FF
;  SIMULATE LOAD START

               SEI
               STX $AE
               STY $AF
               JSR INIT
               JSR FINDFILE
               BCS .FILEFOUND
               LDA #$1b
               STA $d011
               JMP main                 ; FILE NOT FOUND SHOW INSERT DISK 1 ET
.FILEFOUND     LDY #$10
               JSR GETBYTE:INY
               STA LOADAD+1
               JSR GETBYTE
               STA LOADAD
               LDY #$1A
               JSR GETBYTE:INY
               STA CLUSTER
               JSR GETBYTE:INY
               STA CLUSTER+1
               JSR GETBYTE:INY
               STA LENGTH+1
               JSR GETBYTE
               STA LENGTH
               JSR CALCFIRST
               JSR SETUPSEC
               JSR SEEK
               LDA LENGTH
               LSR 
               ADD #1
               STA TTLSEC
;  SUB #1
               LDA LENGTH
               STA BYTESLEFT+1
               LDA LENGTH+1
               STA BYTESLEFT
               LDA LOADAD+1
               STA $AF
               STA TO+1
               LDA LOADAD
               STA $AE
               STA TO

               JMP $0200

GETBYTE        LDX #$37
               LDA #$30
               STA 1
               LDA ($FB),Y
               STX 1
               RTS



PRG            JSR READSECTORS
               LDA #$35
               STA 1
               LDA #$1B
               STA $D011
;.LOOP          INC $D020
;             LDA #1
;            BNE .LOOP
               JMP (STARTUP)            ; LOAD ADDRESS OF PROGRAM


CLRSCRN        LDA #$93
               JSR $FFD2
               SEI
               RTS

DECOMP
               LDA #$04
               STA $fc
               LDA #0
               STA $FB
               LDA #$1B
               STA $FE
               LDA #$A3
               STA $FD
.LOOP          JSR GETDATA
               CMP #$FB
               BCS .COMMAND
               JSR PUTDATA
               JMP .LOOP

.COMMAND       CMP #$FB
               BEQ .REPT
               CMP #$FE
               BEQ .CHNGCOL
               RTS
.CHNGCOL
               JSR GETDATA
               STA COLOUR
               JMP .LOOP

.REPT          JSR GETDATA
               TAX
               JSR GETDATA
.REP           PHA
               JSR PUTDATA
               PLA
               DEX
               BNE .REP
               JMP .LOOP


PUTDATA
               LDY #0
               STA ($FB),Y
               LDA $FC
               ADD #$D4
               STA $FC
               LDA COLOUR
               STA ($FB),Y
               LDA $FC
               SUB #$D4
               STA $FC
               INC $FB
               BNE .NOHI
               INC $FC
.NOHI          RTS


GETDATA        LDY #0
               LDA ($FD),Y
               INC $FD
               BNE .NOHI
               INC $FE
.NOHI          RTS




PRINTSCR
               LDY #40*3
.OOP           LDA LINE1,Y
               STA $0400+11*40,Y
               LDA #1
               STA $D800+11*40,Y
               DEY
               BNE .OOP
               RTS

PRINTSC2
               LDY #0
.OOP           LDA LINE4,Y
               STA $0400+11*40,Y
               LDA #1
               STA $D800+11*40,Y

               DEY
               BNE .OOP
               LDY #51+$28
.OOP2          LDA LINE5-1,Y
               STA $0500+11*40-1,Y
               LDA #1
               STA $D900+11*40-1,Y
               DEY
               BNE .OOP2
               RTS

PRINTSC3
               LDY #0
.OOP           LDA LINE2,Y
               STA $0400+11*40,Y
               LDA #1
               STA $D800+11*40,Y
               DEY
               BNE .OOP
               LDY #80+12
.OOP2          LDA LINE3-1,Y
               STA $0500+11*40-1,Y
               LDA #1
               STA $D900+11*40-1,Y
               DEY
               BNE .OOP2


               RTS


GETKEYS
               LDA $DC01
               AND $DC00
               STA JOY
               CMP OLDJOY
               BEQ .NOK
               STA OLDJOY
               AND #$10
               BNE .NOFIR
               SEC
               RTS

.NOFIR         LDA JOY
               EOR #3
               AND #$03
               BEQ .NOK
               LDA #0
               STA COLNUM
               STA TICK
               JSR PULSE
               LDA JOY
               AND #1
               CMP #1
               BEQ .DOWN
               LDA FILENUM
               BEQ .NOK2
               DEC FILENUM
.NOK           CLC
               RTS
.DOWN          LDA FILENUM
               CMP MAX
               BEQ .NOK3
               INC FILENUM
               CLC
               RTS
.NOK3          LDA #0
               STA FILENUM
               CLC
               RTS
.NOK2          LDA MAX
               STA FILENUM
               CLC
               RTS




FILEPTRS       DFW F1,F2,F3,F4,F5,F6,F7,F8,F9,F10,F11,F12,F13,F14,F15

FILELENS       DFB F2-F1,F3-F2,F4-F3,F5-F4,F6-F5,F7-F6,F8-F7
               DFB F9-F8,F10-F9,F11-F10,F12-F11,F13-F12,F14-F13
               DFB F15-F14,F16-F15
START          DFW $0800,$1000,$C000,$0880,$0b09,$445C,$0b09
               DFW $0B09,$1000,$1000,$1000,$0800,$0800,$1000,$0800

PRGNAMES
F1             DFB "GUTZ.EXE"           ; GUTZ  CODE2
F2             DFB "FIRE.PRG"           ; ECKYTHUMP
F3             DFB "MOUSE0.EXE"
F4             DFB "NINJA.EXE"
F5             DFB "PYJAMAS.PRG"
F6             DFB "QUACK.PRG"
F7             DFB "FROSTY.PRG"
F8             DFB "EQUINOX.PRG"
F9             DFB "DISKMON.EXE"
F10            DFB "DISKASC.EXE"
F11            DFB "DISKHEX.EXE"
F12            DFB "DISKCOPY.EXE"
F13            DFB "FILECOPY.EXE"
F14            DFB "BROWSER.EXE"
F15            DFB "FORMAT.EXE"
F16
OLDJOY         DFB 0
JOY            DFB 0
FILENUM        DFB 0
LONAM          DFB 0
HINAM          DFB 0
NAMLEN         DFB 0
COLNUM         DFB 0
TICK           DFB 7
TOCK           DFB 0
COLOUR         DFB 0
KEYT           DFB 0
MAX            DFB 0
LOC
               DFW $D800+11*40
               DFW $D800+12*40
               DFW $D800+13*40
               DFW $D800+14*40
               DFW $D800+15*40
               DFW $D800+16*40
               DFW $D800+17*40
               DFW $D800+18*40
               DFW $D800+19*40
               DFW $D800+20*40
               DFW $D800+21*40

COL            DFB 1,15,12,11,12,15,1,0

LINE1
               DFC "            (1)    UTILITIES            "
               DFC "            (2)      GAMES              "
               DFC "       PLEASE SELECT   1 OR 2           "

LINE2          DFC "            (1) GUTZ                    "
               DFC "            (2) FIREFLY                 "
               DFC "            (3) MOUSETRAP               "
               DFC "            (4) NINJA                   "
               DFC "            (5) PYJAMARAMA              "
               DFC "            (6) QUACK                   "
               DFC "            (7) "
LINE3          DFC "FROSTY                  "
               DFC "            (8) EQUINOX                 "
               DFC "            (9) TOP MENU"

LINE4          DFC "         (1) BASIC                      "
               DFC "         (2) DISCMON                    "
               DFC "         (3) DISCASC (TEXT DISPLAY)     "
               DFC "         (4) DISCHEX (SHOW HEX FILE)    "
               DFC "         (5) DISCCOPY 3.5 TO 3.5        "
               DFC "         (6) FILE COPY DEVICE TO DEVICE "
               DFC "         (7) BROW"
LINE5          DFC "WSER                    "
               DFC "         (8) DISK FORMAT                "
               DFC "         (9) TOP MENU       "
